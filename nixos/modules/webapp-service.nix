flake:
{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.services.movie-club.webapp;
in
{
  options.services.movie-club.webapp = {
    enable = lib.mkEnableOption "movie club webapp";

    package = lib.mkOption {
      type = lib.types.package;
      default = flake.packages.${pkgs.system}.movie-club.webapp;
      description = "Movie club webapp package to run";
    };

    port = lib.mkOption {
      type = lib.types.int;
      default = 80;
      description = "TCP port used by the HTTP server";
    };

    data_file = lib.mkOption {
      type = lib.types.str;
      description = "Path to the JSON file generated by the scraper";
    };
  };

  config = lib.mkIf cfg.enable {
    systemd.services.movie-club-webapp = {
      description = "Movie club webapp";
      wantedBy = [ "multi-user.target" ];
      after = [
        "movie-club-scraper.service"
        "network.target"
      ];
      serviceConfig = {
        DynamicUser = true;
        Type = "simple";
        ExecStart = "${lib.getExe cfg.package}";
      };
      environment = {
        PORT = builtins.toString config.services.movie-club.webapp.port;
        DATA_FILE_PATH = config.services.movie-club.webapp.data_file;
      };
    };

    networking.firewall.allowedTCPPorts = [ config.services.movie-club.webapp.port ];
  };
}
